import 'time_limits.eol';
import 'transactions_per_sec.eol';

wizard InferPerformanceAnnotations {
  title : 'Infer performance annotations'
  do {
    transaction {
      var success := true;

      for (activityStart in ActivityStart.all) {
        if (not annotateTimeLimits(activityStart)
            or not annotateTransactionsPerSec(activityStart))) {
           success := false;
           break;
        }
      }

      if (not success) {
        System.user.inform('The  performance annotation failed. Rolling back...');
        abort;
      }
    }
  }
}

wizard RemoveAllInferredAnnotations {
  title : 'Remove all inferred annotations'
  do {
    for (constraint in ActionPerformanceAnnotation.all.select(r|not r.manuallyAdded)) {
      delete constraint;
    }
  }
}

operation ActivityAction createPerformanceAnnotation() {
  self.annotation := new ActionPerformanceAnnotation;
  self.annotation.manuallyAdded := false;
  self.activity.nodes.add(self.annotation);
}

operation ActivityStart getGlobalTimeLimit() {
  return self.activity.annotation.secsTimeLimit;
}

@cached
operation ActivityStart getTransactionsPerSec() : Real {
  return self.activity.annotation.transactionsPerSec;
}
