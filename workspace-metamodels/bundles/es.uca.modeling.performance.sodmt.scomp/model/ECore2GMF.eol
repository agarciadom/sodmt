// ACTIVITY PARTITIONS
var partFD = findFigureDescriptor('ActivityPartitionFigure');
var partF = partFD.actualFigure;
var oldLabel = partF.children.selectOne(f|f.name='ActivityPartitionLabelFigure');
var oldCompartment = partF.children.selectOne(f|f.name='ActivityPartitionNodesCompartmentFigure');

// Remove the padding added by the RoundedRectangleFigure, which is half of the corner width/height
partF.border.insets.top    = -4;
partF.border.insets.bottom = -4;

// Replace the old label with a VerticalLabel
var newLabel = new GmfGraph!VerticalLabel;
newLabel.name = oldLabel.name;
newLabel.text = oldLabel.text;
newLabel.maximumSize = oldLabel.maximumSize;
partF.children.add(0, newLabel);
delete oldLabel;
var labelAccessor = partFD.accessors.selectOne(a|a.accessor = 'getFigureActivityPartitionLabelFigure');
labelAccessor.figure = newLabel;

// Replace the old compartment with an InvisibleRectangle
var compartment = new GmfGraph!InvisibleRectangle;
compartment.name = oldCompartment.name;
delete oldCompartment;
partF.children.add(compartment);
var compartmentAccessor = partFD.accessors.selectOne(a|a.accessor = 'getActivityPartitionNodesCompartmentFigure');
compartmentAccessor.figure = compartment;

// Use a grid-based layout
partF.layout = new GmfGraph!GridLayout;
partF.layout.numColumns = 2;
partF.layout.equalWidth = false;

newLabel.layoutData = new GmfGraph!GridLayoutData;
newLabel.layoutData.grabExcessVerticalSpace = true;
newLabel.layoutData.verticalAlignment = GmfGraph!Alignment#FILL;

compartment.layoutData = new GmfGraph!GridLayoutData;
compartment.layoutData.grabExcessHorizontalSpace = true;
compartment.layoutData.grabExcessVerticalSpace = true;
compartment.layoutData.horizontalAlignment = GmfGraph!Alignment#FILL;
compartment.layoutData.verticalAlignment = GmfGraph!Alignment#FILL;
compartment.border = new GmfGraph!CustomBorder;
compartment.border.qualifiedClassName = 'serviceComposition.diagram.borders.LeftBorder';

// ACTION NODES
findFigure('ActionFigure').centerContents();
findFigure('ObjectNodeFigure').centerContents();

operation findFigureDescriptor(name : String) {
  return GmfGraph!FigureDescriptor.all.selectOne(f|f.name = name);
}

operation findFigure(name : String) {
  return GmfGraph!Shape.all.selectOne(f|f.name = name);
}

$pre self.children.size() = 1
operation GmfGraph!Shape centerContents() {
  self.layout = new GmfGraph!GridLayout;

  var child = self.children.first;
  child.layoutData = new GmfGraph!GridLayoutData;
  child.layoutData.grabExcessVerticalSpace = true;
  child.layoutData.grabExcessHorizontalSpace = true;
}
