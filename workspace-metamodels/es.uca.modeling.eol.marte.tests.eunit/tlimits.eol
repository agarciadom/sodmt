import 'test-common.eol';
import 'platform:/resource/es.uca.modeling.eol.marte/eol/time_limits.eol';

$onlyWith Map { "Model" = "UML" }
$onlyWith Map { "Model" = "MultiDiagram" }
@test
operation handleOrderAnnotations() {
  var inferredTL := inferTimeLimits("Handle Order", 1, true);
  assertTimeLimit("Evaluate Order", 0.4, inferredTL);
  assertTimeLimit("Create Invoice", 0.2, inferredTL);
  assertTimeLimit("Perform Payment", 0.2, inferredTL);
  assertTimeLimit("Ship Order", 0.4, inferredTL);
  assertTimeLimit("Close Order", 0.2, inferredTL);
}

$onlyWith Map { "Model" = "MultiDiagram" }
@test
operation evaluateOrderAnnotations() {
  var inferredTL := inferTimeLimits("Evaluate Order", 1, true);
  assertTimeLimit("Create Order", 0.2, inferredTL);
  assertTimeLimit("List Warehouses", 0.2, inferredTL);
  assertTimeLimit("LoopThroughOrderItems", 0.2, inferredTL);
  assertTimeLimit("CheckIfWarehouseHasStock", 0.1, inferredTL);
  assertTimeLimit("Select Next Order Item", 0.1, inferredTL);
  assertTimeLimit("Check Stock", 0.05, inferredTL);
  assertTimeLimit("Remove Warehouse", 0.05, inferredTL);
  assertTimeLimit("Accept Order", 0.2, inferredTL);
  assertTimeLimit("Select Closest Warehouse", 0.2, inferredTL);
  assertTimeLimit("Reject Order", 0.4, inferredTL);
}

$onlyWith Map { "Model" = "UML" }
@test
operation seqWithRepsAnnotations() {
  var inferredTL := inferTimeLimits("SequenceWithRepetitions", 1, true);
  assertTimeLimit("A", 0.125, inferredTL);
  assertTimeLimit("B", 0.25,  inferredTL);
  assertTimeLimit("C", 0.125, inferredTL);
}

$onlyWith Map { "Model" = "UML" }
@test
operation handleOrderSW() {
  inferTimeLimits("Handle Order", 1, true);
  var ctxParamMap := buildContextParameterMap();
  var expectedParamMap := Map { "swEO" = 0.0, "swCI" = 0.2, "swPP" = 0.2, "swSO" = 0.4, "swCO" = 0.2 };
  assertContextParamsEqual(expectedParamMap, ctxParamMap);
}

operation assertTimeLimit(name : String, timeLimit : Real, tlMap : Map) {
  assertEquals(name + " should have " + timeLimit + "s", timeLimit, tlMap.get(name), 5);
}

operation assertContextParamsEqual(expectedParamMap : Map, ctxParamMap : Map) {
  assertEquals("There should be " + expectedParamMap.size + " context parameters", expectedParamMap.size, ctxParamMap.size);
  for (param in expectedParamMap.keySet) {
    var expected := expectedParamMap.get(param);
    var obtained := ctxParamMap.get(param);
    assertEquals("The " + param + " context parameter should be equivalent to " + expected, expected, obtained, 5);
  }
}

operation buildContextParameterMap() : Map {
  var vslTool := Activity.all.first.getVSLTool();
  return Activity.all
                  .collect(a | a.stereotypeApplications).flatten
                  .collect(c : GaAnalysisContext | c.context).flatten
                  .collect(c | vslTool.parseContextParameter(c))
                  .aggregate(c | c.left, c.right);
}

operation buildTimeLimitMap() : Map {
  var vslTool := Activity.all.first.getVSLTool();
  var result : Map;
  for (execNode in ExecutableNode.all) {
    var kvCalc := execNode.stereotypeApplications
                          .collect(c : GaScenario | c.hostDemand)
                          .flatten
                          .collect(h | vslTool.getKeyvalMap(h))
                          .selectOne(m | m.get("source") = "calc");
    if (kvCalc.isDefined()) {
      result.put(execNode.name, kvCalc.get("value").asReal());
    }
  }
  return result;
}

operation inferTimeLimits(activityName : String, globalLimit : Real, shouldSucceed : Boolean) {
  assertEquals(shouldSucceed, distributeTime(globalLimit, getFinalNodes(activityName)));
  return buildTimeLimitMap();
}
