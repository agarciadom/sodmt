import 'time_limits.eol';
import 'concurrent_users.eol';

wizard InferPerformanceAnnotations {
  title : 'Infer performance annotations'
  do {
    transaction {
      for (start in ProcessStart.all) {
        if (not annotateTimeLimits(start)
            or not annotateConcurrentUsers(start)) {
          abort;
          return;
        }
      }
    } -- transaction
  }
}

wizard InferTimeLimitsNew {
  title : 'Infer time limits (new algorithm)'
  do {
    transaction {
      for (start in ProcessStart.all) {
        if (not annotateTimeLimitsNew(start)) {
          abort;
          return;
        }
      }
    } -- transaction
  } -- do
}

wizard RemoveAllInferredAnnotations {
  title : 'Remove all inferred annotations'
  do {
    for (constraint in ActivityPerformanceAnnotation.all.select(r|not r.manuallyAdded)) {
      delete constraint;
    }
  }
}

operation ServiceActivity createPerformanceAnnotation() {
  self.annotation := new ActivityPerformanceAnnotation;
  self.annotation.manuallyAdded := false;
  ServiceProcess.all.first().activityPerformance.add(self.annotation);
}

operation ProcessStart getGlobalTimeLimit() {
  return ProcessPerformanceAnnotation.all.first().secsTimeLimit;
}

@cached
operation ProcessStart getConcurrentUsers() : Real {
  return ProcessPerformanceAnnotation.all.first().concurrentUsers;
}
