import "common.eol";

@data wA
@data wB
@data wC
@data wD
operation weights() {
  return 0.to(5);
}

@data mA
@data mB
@data mC
@data mD
operation minTimes() {
  return Sequence {0, 30, 60};
}

@model
operation createModel() {
  var sp = new ServiceProcess;

  var start = new InitialNode;
  var decAB = new DecisionNode;
  var decCD = new DecisionNode;
  var mrgAB = new JoinNode;
  var mrgCD = new JoinNode;
  var fin = new FinalNode;
  sp.nodes.add(start);
  sp.nodes.add(decAB); sp.nodes.add(mrgAB);
  sp.nodes.add(decCD); sp.nodes.add(mrgCD);
  sp.nodes.add(fin);

  var nodeA = createAction(sp, "A", wA, mA);
  var nodeB = createAction(sp, "B", wB, mB);
  var nodeC = createAction(sp, "C", wC, mC);
  var nodeD = createAction(sp, "D", wD, mD);

  createEdge(sp, start, decAB);
  createDiamondEdges(sp, decAB, nodeA, nodeB, mrgAB);
  createEdge(sp, mrgAB, decCD);
  createDiamondEdges(sp, decCD, nodeC, nodeD, mrgCD);
  createEdge(sp, mrgCD, fin);
}

@test
operation correctResults() {
  var globalLimit := 200;
  var epsilon     := 0.0001;
  var startNode   := getStartNode();
  annotateTimeLimits(globalLimit, startNode);
  for (path in getAllExecutionPathsFrom(startNode)) {
    var total := path.collect(a : ExecutableNode | a.getTimeLimit()).sum;
    if (total > globalLimit + epsilon) {
      fail("Total was " + total + ", which is greater than " + globalLimit);
    }
  }
}

operation getStartNode() {
  return ActivityNode.allInstances.select(e|e.incoming.isEmpty()).first();
}

operation createAction(sp : ServiceProcess, name : String, weight : Real, minLimit : Real) : Action {
  var node = new Action;
  node.name = name;
  sp.nodes.add(node);

  node.createPerformanceAnnotation();
  node.annotation.weight      := weight.asReal();
  node.annotation.minimumTime := minLimit.asReal();
  return node;
}

operation createEdge(sp, source, target) : ControlFlow {
  var edge = new ControlFlow;
  edge.source := source;
  edge.target := target;
  sp.edges.add(edge);
  return edge;
}

operation createDiamondEdges(sp, decision, nodeA, nodeB, merge) : Sequence {
  return Sequence {
  	createEdge(sp, decision, nodeA),
  	createEdge(sp, decision, nodeB),
  	createEdge(sp, nodeA, merge),
  	createEdge(sp, nodeB, merge)
  };
}
