-*
  Miscellaneous utilities for both inference algorithms.

  Copyright (c) 2009-2012 Antonio García Domínguez.  All rights
  reserved. This program and the accompanying materials are made
  available under the terms of the Eclipse Public License v1.0 which
  accompanies this distribution, and is available at
  http://www.eclipse.org/legal/epl-v10.html

  Contributors:
     Antonio García Domínguez - initial API and implementation
*-

-- CONSTRAINTS ----------------------------------------------------------------

-*
  Computes the slack per unit of weight of a path using its constraint, when
  the path receives T seconds.
*-
operation Sequence slackPerWU(T : Real) : Real {
  if (self.last() > 0) {
    return (T-self.first())/self.last();
  } else {
    return 0;
  }
}

-- GRAPH PREPROCESSING --------------------------------------------------------

@cached
$post _result >= 0
operation ActivityNode getDepth() : Integer {
  if (self.incoming.isEmpty()) {
    return 0;
  }
  else {
    return self.incoming.collect(r|r.source.getDepth()).max() + 1;
  }
}

-- LOWEST COMMON ANCESTOR -----------------------------------------------------

@cached
$pre nodeA.getDepth().isDefined() and nodeB.getDepth().isDefined()
operation getLCA(nodeA : ActivityNode, nodeB : ActivityNode) : ActivityNode {
  var depths     := Sequence { nodeA.getDepth(), nodeB.getDepth() };
  var maxDepth   := depths.min();
  var ancestorsA := nodeA.getAncestors(maxDepth);
  var ancestorsB := nodeB.getAncestors(maxDepth);
  var common := ancestorsA.intersect(ancestorsB);

  if (common.isEmpty()) {
    return;
  }

  return common.asSequence().sortBy(r|-r.getDepth()).first();
}

$pre maxDepth >= 0
operation ActivityNode getAncestors(maxDepth : Integer) : Set {
  var results := new Set;
  self.addAllAncestors(maxDepth, results);
  return results;
}

$pre maxDepth >= 0 and results.isDefined()
operation ActivityNode addAllAncestors(maxDepth : Integer, results : Collection) {
  if (self.getDepth() <= maxDepth) {
    results.add(self);
  }
  for (parent in self.getPrev()) {
    parent.addAllAncestors(maxDepth, results);
  }
}

$pre otherSet.isDefined()
operation Set intersect(otherSet : Set) : Set {
  if (otherSet.size() < self.size()) {
    return otherSet.intersect(self);
  }

  var results : Set;
  for (element in self.select(r|otherSet.includes(r))) {
    results.add(element);
  }
  return results;
}

----- NODE TRAVERSAL ----------------------------------------------------------

@cached
operation ActivityNode getLoopPart() : Integer {
  if (self.inStructuredNode.isDefined() and self.inStructuredNode.isKindOf(LoopNode)) {
    return self.getLoopPart(self.inStructuredNode);
  }
  else {
    return -1;
  }
}

@cached
operation ActivityNode getLoopPart(ln : LoopNode) : Integer {
  if (ln.setupPart.contains(self)) {
    return 0;
  }
  else if (ln.test.contains(self)) {
    return 1;
  }
  else if (ln.bodyPart.contains(self)) {
     return 2;
  }
  else {
    return 3;
  }
}

-- Returns all the nodes that are directly after this one: normally it will use
-- the outgoing edges, but it may need to work differently in some cases.
@cached
operation ActivityNode getNext() : Set {
  if (self.outgoing.isEmpty()) {
    for (loopNode in self.inStructuredNode.select(s : LoopNode | true)) {
      var part := self.getLoopPart(loopNode);
      if (part == 0 and not loopNode.test.isEmpty()) {
        return loopNode.test.getSources();
      }
      else if ((part == 0 or part == 1) and not loopNode.bodyPart.isEmpty()) {
        return loopNode.bodyPart.getSources();
      }
    }
  } else {
    return self.outgoing.collect(e|e.target).asSet();
  }
  return Set {};
}

-- Returns all the nodes that are directly after this one: normally it will use
-- the outgoing edges, but it may need to work differently in some cases.
@cached
operation ActivityNode getPrev() : Set {
  if (self.incoming.isEmpty()) {
    for (loopNode in self.inStructuredNode.select(s : LoopNode | true)) {
      var part := self.getLoopPart(loopNode);
      if (part == 2 and not loopNode.test.isEmpty()) {
        return loopNode.test.getSinks();
      }
      else if ((part == 1 or part == 2) and not loopNode.setupPart.isEmpty()) {
        return loopNode.setupPart.getSinks();
      }
    }
  } else {
    return self.incoming.collect(e|e.source).asSet();
  }
  return Set {};
}

operation Activity getSources() : Set {
  return self.nodes.getSources();
}

operation Activity getSinks() : Set {
  return self.nodes.getSinks();
}

operation LoopNode getSources() : Set {
  if (not self.setupPart.isEmpty()) { return self.setupPart.getSources(); }
  else if (not self.test.isEmpty()) { return self.test.getSources(); }
  else if (not self.bodyPart.isEmpty()) { return self.bodyPart.getSources(); }
  else { return self.nodes.getSources(); }
}

operation LoopNode getSinks() : Set {
  if (not self.bodyPart.isEmpty()) { return self.bodyPart.getSinks(); }
  else if (not self.test.isEmpty()) { return self.test.getSinks(); }
  else if (not self.setupPart.isEmpty()) { return self.setupPart.getSinks(); }
  else { return self.nodes.getSinks(); }
}

operation StructuredActivityNode getSources() : Set {
  return self.nodes.getSources();
}

operation StructuredActivityNode getSinks() : Set {
  return self.nodes.getSinks();
}

operation ActivityNode getSources() : Set {
  return Set {};
}

operation ActivityNode getSinks() : Set {
  return Set {};
}

operation Collection getSources() : Set {
  return self.select(n | n.incoming.select(c : ControlFlow | true).isEmpty()).asSet();
}

operation Collection getSinks() : Set {
  return self.select(n | n.outgoing.select(c : ControlFlow | true).isEmpty()).asSet();
}

@cached
operation ActivityNode getContainers() : Set {
  if (self.activity.isDefined()) {
    return Set { self.activity };
  } else {
    return self.inGroup;
  }
}

----- REACHABLE NODES ---------------------------------------------------------

-- Obtains all the nodes reachable from a node in the graph, in no
-- particular order. Returns a Set: to convert it to a sequence, use asSequence().
-- Note that if you only want the activities, you will need to filter them later
-- on, using select(), for instance.
operation ActivityNode getAllReachableNodes() : Set {
  var nodeSet := new Set;
  nodeSet.add(self);
  self.addAllReachableNodes(nodeSet);
  return nodeSet;
}

-- Adds all the nodes which are reachable from a node in the graph to a set of
-- nodes. Avoids nodes which have been already visited.
operation ActivityNode addAllReachableNodes(nodeSet : Set) {
  addAllReachableNodeSet(self.getSources(), nodeSet);
  addAllReachableNodeSet(self.getNext(), nodeSet);
}

operation addAllReachableNodeSet(source : Collection, target : Collection) {
  for (node in source) {
    if (target.add(node)) {
      node.addAllReachableNodes(target);
    }
  }
}

-- COMPARISON OF REAL NUMBERS -----------------------------

$pre epsilon > 0
operation isApproximatelyEqual(a, b, epsilon) : Boolean {
  return (a - b).abs() <= epsilon;
}

-- COLLECTION UTILITIES -----------------------------------

$pre not self.isEmpty()
operation Sequence popFirst() : Any {
  var elem = self.first();
  self.removeAt(0);
  return elem;
}

$pre not self.isEmpty()
operation OrderedSet popFirst() : Any {
  var elem = self.first();
  self.removeAt(0);
  return elem;
}

$pre not self.isEmpty()
operation Sequence popLast() : Any {
  var elem = self.last();
  self.removeAt(self.size()-1);
  return elem;
}

$pre not self.isEmpty()
operation OrderedSet popLast() : Any {
  var elem = self.last();
  self.removeAt(self.size()-1);
  return elem;
}