/*
  Heuristic algorithm for computing the time limits in seconds for
  each ExecutableNode node in the ServiceComposition and ServiceProcess
  models.

  Copyright (c) 2009-2022 Antonio García Domínguez.  All rights
  reserved. This program and the accompanying materials are made
  available under the terms of the Eclipse Public License v2.0 which
  accompanies this distribution, and is available at
  http://www.eclipse.org/legal/epl-v10.html

  Contributors:
     Antonio García Domínguez - initial API and implementation

  This algorithm annotates every executable node with an estimation of
  the maximum time that it can take to run so the whole service
  ExecutableNode is completed before a specific time interval has
  elapsed. If the node already has a manual restriction applied to it,
  it will also make sure that a consistent solution can be achieved.
  Unless explicitly told otherwise, it assumes that all executable
  nodes in every path should take around the same time. The actual time
  will vary depending on the structure of the graph and the manual
  time constraints.

  IMPORTANT: the model must have been validated with the EVL script in
  es.uca.modeling.validation and pass with no errors.

  The algorithm follows these steps:

  - Remove all previously computed local time limits.

  - Obtain a list of all paths between the initial node and the final
    nodes. It would be impossible to get a finite list out of a graph
    with cycles, but the EVL validation script ensures the graph is
    acyclic, precisely for this reason.

  - Sort the list in descending order by an estimation of the time
    which would be assigned to each node in the path in absence of
    other paths. This ensures the algorithm produces correct results
    and saves us from having to look at previous paths while annotating
    some path, since the previous paths were always at least as restrictive
    as the current one.

  - For each path in the list:

    * Calculate the initial slack as the difference between the global
      time limit and the sum of the manually set time limits in the
      path.

    * If the slack is negative, report an error and abort. The sum of
      the manual annotations exceeded the time limit which had been
      set for the whole path in the Service ExecutableNode.

    * If the slack is zero and there exist free nodes in the path,
      report an error and abort. Every node needs to have a time limit
      greater than zero.

    * Otherwise, if there are free nodes in the path, split up the slack
      between them. If this is the first path, no free nodes will be annotated,
      so all the slack can be distributed. Otherwise, some free nodes will be
      already annotated from the previous paths. Since the previous paths were
      at least as restrictive as this one, we can subtract their annotated time
      limits from the slack and split the remaining slack among the nodes which
      have not been annotated yet.
*/
import 'utils.eol';

/*
   Current assumptions: acyclic graph with only one initial node
   Output: returns true when all nodes reachable from the starting node
           are successfully annotated, false otherwise.
*/
$pre start.isDefined()
operation annotateTimeLimits(global_limit : Real, start : ActivityNode) : Boolean {
  var paths        := getAllExecutionPathsFrom(start);

  // Cache all manual time limits and relax all automatic constraints
  for (path in paths) {
    for (node in path) {
      node.setTimeLimit(0.0d);
    }
    path.~minSum     := getMinTimeSum(path);
    path.~startingSlack := global_limit - path.~minSum;
    path.~totalWeight   := getWeightSum(path);
  }

  // Use the paths to establish the constraints, using the most restrictive
  // paths first. Otherwise, results would vary depending on what path got
  // evaluated first, and would be inconsistent.
  for (path in paths.sortBy(p | p.getLimitEstimateForFreeNodes())) {
    var slack := path.~startingSlack;
    var totalWeight := path.~totalWeight;

    if (isApproximatelyEqual(slack, 0.0, 0.0001)) {
      if (totalWeight > 0) {
        System.user.inform('Nodes with manual restrictions in path '
          + path.select(r|r.isKindOf(ExecutableNode)).collect(r|r.name).asString()
          + ' left no slack to establish the automatic restrictions (had '
          + global_limit + ' seconds)');
        return false;
      }
    } else if (slack < 0) {
      System.user.inform('Nodes with manual restrictions in path '
        + path.select(r|r.isKindOf(ExecutableNode)).collect(r|r.name).asString()
        + ' have used up more than the available time ('
        + path.~minSum + ', with ' + global_limit + ' available)');
      return false;
    } else {
      splitSlackOverFreeNodes(path, global_limit);
    }
  }

  return true;
}

// Returns an estimate of the time available for each free node in the path.
// If there are no free nodes, it will use an arbitrarily large value to ensure
// that it is one of the last ones. We only want paths with no free nodes for
// consistency checking, anyway.
operation Sequence getLimitEstimateForFreeNodes() : Real {
  if (self.~totalWeight = 0) {
    return 1000000;
  } else {
    return self.~startingSlack/self.~totalWeight;
  }
}

$pre path.isDefined() and path.notEmpty() and global_limit > 0
operation splitSlackOverFreeNodes(path : Sequence, global_limit : Real) {
  /*
    There are two possibilities:
     * this is the first path in the graph, so all free nodes can be updated
       to the new time limit.
     * some free nodes were already annotated by paths which were at least as
       restrictive as this one, so we can safely ignore them. Use the remaining
       slack for the nodes which have not been annotated yet.
  */
  var unrestricted := getUnrestrictedNodes(path, global_limit);
  if (unrestricted.notEmpty()) {
    var prevLimitsSum := path.collect(node | node.getTimeLimit()).sum();
    var minTimeSum := getMinTimeSum(unrestricted);
    var weightSum  := getWeightSum(unrestricted) * 1.0d;
    var slack      := global_limit - (prevLimitsSum + minTimeSum);
    var slackPerWU := 0;
    if (weightSum > 0) {
      slackPerWU := slack / weightSum;
    }

    for (node  in unrestricted) {
        var newLimit := node.getMinimumTime() + node.getWeight() * slackPerWU;
        node.setTimeLimit(newLimit);
    }
  }
}

// TIME LIMITS: QUERIES AND UPDATES

operation getMinTimeSum(path : Sequence) : Real {
  return path.select(node | node.hasProperty("annotation")).collect(r | r.getMinimumTime() * r.getRepetitions()).sum();
}

operation getWeightSum(path : Sequence) : Real {
  return path.select(node | node.hasProperty("annotation")).collect(r|r.getWeight() * r.getRepetitions()).sum();
}

// RESTRICTED/UNRESTRICTED NODES

// Returns all nodes in the path which do not have a performance restriction yet.
operation getUnrestrictedNodes(path : Sequence, global_limit) : Sequence {
  return path.select(node | node.hasProperty("annotation") and not isRestricted(node));
}

// Returns all nodes in the path which already have a performance restriction.
operation getRestrictedNodes(path : Sequence, global_limit) : Sequence {
  return path.select(node | node.hasProperty("annotation") and isRestricted(node));
}

// Returns true if the node has a performance restriction, either manual or
// automatic. Automatic restrictions which are equal to the specified dummy
// value are considered not to exist.
operation isRestricted(node) : Boolean {
  return node.annotation.isDefined() and node.annotation.secsTimeLimit > 0;
}
