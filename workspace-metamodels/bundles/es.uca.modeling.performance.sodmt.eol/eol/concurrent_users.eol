/*
  Algorithm for computing the concurrent users expected for each
  activity node in the ServiceComposition and ServiceProcess
  models.

  Copyright (c) 2009-2022 Antonio García Domínguez.  All rights
  reserved. This program and the accompanying materials are made
  available under the terms of the Eclipse Public License v1.0 which
  accompanies this distribution, and is available at
  http://www.eclipse.org/legal/epl-v10.html

  Contributors:
     Antonio García Domínguez - initial API and implementation

  The algorithm computes the concurrent users of every node and
  edge, but only adds annotation model elements to the executable
  nodes. IMPORTANT: the model must have been validated with the EVL
  script in es.uca.modeling.validation and pass with no errors.

  The algorithm works like this:

    Starting from the initial node, we perform a breadth-first
    traversal of the graph, making sure that each node is only visited
    once all its ancestors have been visited (i.e. annotated with the
    computed concurrent users). The computed value depends on
    the kind of node visited. If C(x) is the concurrent users for
    any node and P(e) the traversal probability annotated on an edge,
    then the formulas are as follows:

    * C(initial node) = T(only ProcessPerformanceAnnotation)

    * C(edge) = P(edge) * T(source)

    * C(node, except JoinNode) = T(only incoming edge)

    * C(JoinNode): we assume that the incoming paths split off at a
      ForkNode. Therefore, the result is the minimum of all the
      concurrent users (for sync).

    * C(MergeNode): in this case, the incoming paths split off at a
      DecisionNode. Therefeore, the result of the sum of all the
      concurrent users (mutually exclusive paths).
*/

import 'utils.eol';

/*
  This operation returns true if all nodes reachable from the starting node
  have been successfully annotated, and false otherwise.
*/
operation annotateConcurrentUsers(start : ActivityNode) : Boolean {
  // Since the precondition must hold, all activities are reachable from the
  // initial node. Get all reachable nodes and filter only the executable nodes.
  var allNodes := start.getAllReachableNodes().asSequence().select(r|r.isKindOf(ExecutableNode));

  for (node in allNodes.sortBy(r|r.getDepth())) {
	var auto := node.getConcurrentUsers();
    node.setConcurrentUsers(auto);
  }
  return true;
}

// AUTOMATIC TRANSACTIONS PER SEC RESTRICTION COMPUTATION

/*
  All nodes and edges have a minimum number of concurrent users
  that they can handle. However, only the executable nodes can have performance
  restrictions manually assigned to them. That's why though all edges and nodes
  have the getConcurrentUsers method, only Activities have the
  setConcurrentUsers method.

  Note: for some reason, Epsilon (as of r597) doesn't use the most specific
  operation for a particular class, but rather the first applicable operation.
  Therefore, the getConcurrentUsers operations below MUST be ordered from
  least to most specific: first the edges, and then the nodes.
*/

@cached
$pre self.source.isDefined() and self.target.isDefined()
operation ControlFlow getConcurrentUsers() : Real {
  return self.probability * self.source.getConcurrentUsers();
}

@cached
$pre self.source.isDefined() and self.target.isDefined()
operation ObjectFlow getConcurrentUsers() : Real {
  return self.source.getConcurrentUsers();
}

@cached
$pre self.incoming.notEmpty()
operation JoinNode getConcurrentUsers() : Real {
  var result;
  for (incomingEdge in self.incoming) {
    if (result.isUndefined()) {
      result = incomingEdge.getConcurrentUsers();
    } else {
      // use minimum (the join waits for the least frequent branch)
      result = result.min(incomingEdge.getConcurrentUsers());
    }
  }
  return result;
}

@cached
$pre self.incoming.notEmpty()
operation MergeNode getConcurrentUsers() : Real {
  var result = 0;
  for (incomingEdge in self.incoming) {
    // use sum (merge brings together mutually exclusive paths)
    result = result + incomingEdge.getConcurrentUsers();
  }
  return result;
}

@cached
operation InitialNode getConcurrentUsers() : Real {
  return self.getGlobalPerformanceAnnotation().concurrentUsers;
}

@cached
$pre self.incoming.size() = 1
operation ActivityNode getConcurrentUsers() : Real {
  return self.incoming.first.getConcurrentUsers();
}

$pre newValue >= 0
operation ExecutableNode setConcurrentUsers(newValue : Real) {
  if (self.annotation.isUndefined()) {
    self.createPerformanceAnnotation();
  }
  self.annotation.concurrentUsers := newValue;
}
