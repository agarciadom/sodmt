import 'time_limits.eol';
import 'transactions_per_sec.eol';

wizard InferPerformanceAnnotations {
  title : 'Infer performance annotations'
  do {
    transaction {
      for (start in ActivityStart.all) {
        if (not annotateTimeLimits(start)
            or not annotateTransactionsPerSec(start)) {
          abort;
          return;
        }
      }
    } -- transaction
  }
}

wizard RemoveAllInferredAnnotations {
  title : 'Remove all inferred annotations'
  do {
    for (constraint in ActionPerformanceAnnotation.all.select(r|not r.manuallyAdded)) {
      delete constraint;
    }
  }
}

operation ActivityAction createPerformanceAnnotation() {
  self.annotation := new ActionPerformanceAnnotation;
  self.annotation.manuallyAdded := false;
  self.activity.nodes.add(self.annotation);
}

operation ActivityStart getGlobalTimeLimit() {
  return self.activity.annotation.secsTimeLimit;
}

@cached
operation ActivityStart getTransactionsPerSec() : Real {
  return self.activity.annotation.concurrentUsers;
}
