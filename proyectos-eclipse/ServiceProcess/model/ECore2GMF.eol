for (n in Sequence {
            'InitialNodeLabel', 'FinalNodeLabel',
            'ForkNodeLabel', 'JoinNodeLabel',
            'DecisionNodeLabel',
            'ProbabilityAnnotationConditionExternalLabel',
            'PerformanceAnnotationExecNodeExternalLabel'}) {
   removeLabel(n);
}

operation removeLabel(diagramLabelName : String) {
  -- no label for InitialNode
  var diagLabel
    = GmfGraph!DiagramLabel.all.selectOne(r|r.name = diagramLabelName);
  var labelMapping
    = GmfMap!LabelMapping.all.selectOne(r | r.diagramLabel = diagLabel);
  
  if (labelMapping.isDefined()) {
    delete labelMapping;
  }
  if (diagLabel.accessor.isDefined()) {
    -- Composite figure: we only want to remove the child label
    delete diagLabel.accessor.figure;
    delete diagLabel.accessor;
  }
  else {
    -- The whole figure is the label: remove it
    delete diagLabel.figure;
  }
  delete diagLabel;
}
